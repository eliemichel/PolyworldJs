<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf8" />
		<title>Polyworld</title>
		<link href="css/style.css" type="text/css" rel="stylesheet" />
	</head>

	<body>
		<script id="injectAfterCommon" type="x-shader/x-vertex">

			/**
			 * A tree is composed of a stacking of "floors" (either trunk or foilage elements)
			 */
			struct TreeConfig {
				// Number of cylinders in the crown (i.e., foilage part of the tree)
				uint crownFloorCount;

				// Overall height of the tree
				float height;

				// Randomness in the crown vertices
				float crownRandomness;

				// Radius of the bottom of the bottom-most crown floor
				float crownBaseRadius;

				// If set to 1, the tree looks like a cone. If less than 1, top part of
				// each crown floor is shrinked so it looks more like a pine tree.
				float crownShrinkFactor;

				// Height of the trunk, which must not be more than the overall height
				float trunkHeight;

				// Radius of the trunk
				float trunkRadius;

				// Randomness in the trunk vertices
				float trunkRandomness;

				// Color of the trunk
				vec3 trunkColor;

				// Colors of the crown floors, from bottom to top
				vec3 crownColors[4];

				// Bend factor along each axis
				vec3 bend;

				// How much floor elements overshoot in height, where 0 means no overshoot
				// and 1 means doubling the height.
				float overshoot;
			};

			/**
			 * Trees from a family are all the possible interpolations between 'presetCount' different presets.
			 */
			struct TreeFamilyConfig {
				// Each tree is an interpolation of preset trees
				TreeConfig presets[2];
				uint presetCount;
			};

			/**
			 * Linearly blend the properties of two trees
			 */
			TreeConfig mixTreeConfig(TreeConfig a, TreeConfig b, float fac) {
				return TreeConfig(
					uint(mix(float(a.crownFloorCount), float(b.crownFloorCount), fac)),
					mix(a.height, b.height, fac),
					mix(a.crownRandomness, b.crownRandomness, fac),
					mix(a.crownBaseRadius, b.crownBaseRadius, fac),
					mix(a.crownShrinkFactor, b.crownShrinkFactor, fac),
					mix(a.trunkHeight, b.trunkHeight, fac),
					mix(a.trunkRadius, b.trunkRadius, fac),
					mix(a.trunkRandomness, b.trunkRandomness, fac),
					mix(a.trunkColor, b.trunkColor, vec3(fac)),
					vec3[4](
						mix(a.crownColors[0], b.crownColors[0], vec3(fac)),
						mix(a.crownColors[1], b.crownColors[1], vec3(fac)),
						mix(a.crownColors[2], b.crownColors[2], vec3(fac)),
						mix(a.crownColors[3], b.crownColors[3], vec3(fac))
					),
					mix(a.bend, b.bend, vec3(fac)),
					mix(a.overshoot, b.overshoot, fac)
				);
			}

			/**
			 * Config for a given floor, extracted from the tree config and floor index
			 */
			 struct TreeFloorConfig {
				vec3 color;
				float height;
				float bottomRadius;
				float topRadius;
				float randomness;

				// Sum of all previous floor's height
				float offset;
			};

			TreeFloorConfig getFloorConfig(TreeConfig config, uint floorIndex) {
				bool isTrunk = floorIndex == 0u;
				bool isHidden = floorIndex > config.crownFloorCount;
				if (isTrunk) {
					return TreeFloorConfig(
						config.trunkColor,
						config.trunkHeight * config.overshoot,
						config.trunkRadius,
						config.trunkRadius,
						config.trunkRandomness,
						0.0
					);
				} else if (!isHidden) {
					uint crownIndex = floorIndex - 1u;
					float crownBottomFac = float(crownIndex) / float(config.crownFloorCount);
					float crownTopFac = float(crownIndex + 1u) / float(config.crownFloorCount);
					uint colorIndex = uint(crownBottomFac * 4.0);
					float crownHeight = config.height - config.trunkHeight;
					return TreeFloorConfig(
						config.crownColors[colorIndex],
						crownHeight / float(config.crownFloorCount) * config.overshoot,
						mix(config.crownBaseRadius, 0.0, crownBottomFac),
						mix(config.crownBaseRadius, 0.0, crownTopFac) * config.crownShrinkFactor,
						config.crownRandomness,
						config.trunkHeight + crownBottomFac * crownHeight
					);
				} else {
					return TreeFloorConfig(
						vec3(0.0),
						0.0,
						0.0,
						0.0,
						0.0,
						0.0
					);
				}
			}

			float rand31(vec3 seed1, float seed2) {
				return rand(vec2(
					rand(seed1.xy),
					rand(vec2(seed1.z, seed2))
				));
			}

			mat2 rotate2(float angle) {
				float c = cos(angle);
				float s = sin(angle);
				return mat2(c, s, -s, c);
			}

			mat4 rotateXZ(float angle) {
				float c = cos(angle);
				float s = sin(angle);
				return mat4(
					c, 0.0, s, 0.0,
					0.0, 1.0, 0.0, 0.0,
					-s, 0.0, c, 0.0,
					0.0, 0.0, 0.0, 1.0
				);
			}

			// Bend on a single axis
			mat4 computeBendMatrixXY(uint crownFloorIndex, float bendFactor, float crownFloorHeight) {
				vec3 bendOffset = vec3(0.0);
				/* Iterative version (easier to read):
				for (uint i = 1u ; i <= crownFloorIndex ; ++i) {
					float angle = float(i) * bendFactor;
					bendOffset.xy += crownFloorHeight * vec2(sin(angle), cos(angle));
				}
				*/
				// Equivalent analytic version (faster to evaluate I think):
				float n = float(crownFloorIndex + 1u);
				float dividend = sin(bendFactor / 2.0);
				if (abs(bendFactor) > 1e-6) {
					bendOffset.xy += crownFloorHeight * vec2(
						-sin(bendFactor * (1.0 - n) / 2.0) * sin(n * bendFactor / 2.0) / sin(bendFactor / 2.0),
						0.5 * cos(bendFactor / 2.0) * sin(bendFactor * n) / sin(bendFactor / 2.0) - 0.5 * (1.0 + cos(bendFactor * n))
					);
				} else {
					bendOffset.xy += crownFloorHeight * vec2(0.0, n - 1.0);
				}
				float bendAngle = n * bendFactor;
				float c = cos(bendAngle);
				float s = sin(bendAngle);
				return transpose(mat4(
					 c, s, 0.0, bendOffset.x,
					-s, c, 0.0, bendOffset.y,
					0.0, 0.0, 1.0, bendOffset.z,
					0.0, 0.0, 0.0, 1.0
				));
			}

			// Bend on all axes
			mat4 computeBendMatrix(uint crownFloorIndex, vec3 bend, float crownFloorHeight) {
				float angle = atan(bend.z, bend.x);
				return (
					rotateXZ(angle) *
					computeBendMatrixXY(crownFloorIndex, length(bend.xz), crownFloorHeight) *
					rotateXZ(-angle) *
					rotateXZ(bend.y * float(crownFloorIndex + 1u))
				);
			}

			
			/* ***** INPUTS ***** */

			uniform float time;
			uniform uint instanceCount;
			uniform TreeFamilyConfig treeFamilyConfig;

			attribute float radius;
			attribute vec2 height;
			attribute uint floorIndex;

			// Instance attributes
			attribute vec3 instancePosition;

		</script>

		<script id="injectAfterColorVertex" type="x-shader/x-vertex">

			/* ***** Tree/Floor Config ***** */

			TreeConfig configA = treeFamilyConfig.presets[0];
			TreeConfig configB = treeFamilyConfig.presets[1];
			float instanceFac = (float(gl_InstanceID) + 0.5) / float(instanceCount);
			TreeConfig config = mixTreeConfig(configA, configB, instanceFac);

			TreeFloorConfig floorConfig = getFloorConfig(config, floorIndex);

			mat2 instanceRotation = rotate2(6.28 * rand(vec2(float(gl_InstanceID) * 0.01, 0.5)));

			mat4 bendMatrix = mat4(1.0);
			if (floorIndex > 0u) {
				float crownHeight = config.height - config.trunkHeight;
				float crownFloorHeight = crownHeight / float(config.crownFloorCount);
				bendMatrix = computeBendMatrix(floorIndex - 1u, config.bend, crownFloorHeight);
				bendMatrix[3].y += config.trunkHeight;
			}

			/* ***** Color ***** */

			vColor.rgb = floorConfig.color;
			vColor *= mix(1.0, 0.5, position.y);

		</script>

		<script id="injectAfterBeginVertex" type="x-shader/x-vertex">

			// Radius
			float radius = mix(floorConfig.bottomRadius, floorConfig.topRadius, position.y);
			float radiusFac = length(position.xz);
			transformed.xz *= radiusFac * radius;

			// Height
			transformed.y *= floorConfig.height;

			// Noise (must be before animation)
			vec3 offset = vec3(
				rand31(transformed.xyz + vec3(float(gl_InstanceID) * 0.1, floorConfig.offset, 0.0), 0.5 / 3.0),
				rand31(transformed.xyz + vec3(float(gl_InstanceID) * 0.1, floorConfig.offset, 0.0), 1.5 / 3.0),
				rand31(transformed.xyz + vec3(float(gl_InstanceID) * 0.1, floorConfig.offset, 0.0), 2.5 / 3.0)
			) - 0.5;

			// Pre-bend instance rotation
			transformed.xz = instanceRotation * transformed.xz;

			// Bend + vertical offset
			transformed = (bendMatrix * vec4(transformed, 1.0)).xyz;

			// Instance transformation
			transformed.xyz += instancePosition;

			// Noise
			transformed.xyz += normalize(offset) * floorConfig.randomness * radius;

		</script>

		<script id="injectAfterBeginNormal" type="x-shader/x-vertex">

			// Pre-bend instance rotation
			objectNormal.xz = instanceRotation * objectNormal.xz;

			// Bend
			objectNormal = mat3(bendMatrix) * objectNormal;

		</script>

		<script type="importmap">
		{
			"imports": {
				"three": "./third_party/three.module.min.js",
				"three/addons/": "./third_party/three/addons/"
			}
		}
		</script>
		<script type="module" src="js/main.js"></script>
	</body>
</html>
